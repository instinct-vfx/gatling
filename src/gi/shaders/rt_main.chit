#extension GL_GOOGLE_include_directive: require
#extension GL_EXT_ray_tracing: require
#extension GL_EXT_nonuniform_qualifier: enable
#extension GL_EXT_samplerless_texture_functions: require
#extension GL_EXT_shader_16bit_storage: require
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#extension GL_EXT_shader_explicit_arithmetic_types_int16: require

#include "rt_payload.glsl"
#include "rt_descriptors.glsl"

#pragma MDL_GENERATED_CODE

#include "rt_shading_state.glsl"

layout(location = PAYLOAD_INDEX_SHADE) rayPayloadInEXT ShadeRayPayload rayPayload;

hitAttributeEXT vec2 baryCoord;

bool russian_roulette(in float random_float, inout vec3 throughput)
{
    float max_throughput = max(throughput.r, max(throughput.g, throughput.b));
    float p = min(max_throughput, PC.rrInvMinTermProb);

    if (random_float > p)
    {
        return true;
    }

    throughput /= p;

    return false;
}

void main()
{
    /* 1. Get hit info. */
    vec2 hit_bc = baryCoord;
    uint hit_face_idx = gl_InstanceCustomIndexEXT + gl_PrimitiveID;
    float hit_t = gl_HitTEXT;

    /* 2. Set up shading state. */
    State shading_state; // Shading_state_material
    setup_mdl_shading_state(hit_face_idx, hit_bc, shading_state);

    // we keep a copy of the normal here since it can be changed within the state by *_init() functions:
    // https://github.com/NVIDIA/MDL-SDK/blob/aa9642b2546ad7b6236b5627385d882c2ed83c5d/examples/mdl_sdk/dxr/content/mdl_hit_programs.hlsl#L411
    const vec3 normal = shading_state.normal;

    vec3 throughput = vec3(rayPayload.throughput);
    vec3 radiance = vec3(rayPayload.radiance);

    uint bounce = (rayPayload.bitfield & 0x7FFFu);
    bool inside = bool(rayPayload.bitfield & 0x8000u);

    bool thin_walled = false;
#ifdef THIN_WALLED
    thin_walled = mdl_thin_walled(shading_state);
#endif
    const bool is_backface = inside && thin_walled;
    const float ior1 = (inside && !thin_walled) ? BSDF_USE_MATERIAL_IOR : 1.0;
    const float ior2 = (inside && !thin_walled) ? 1.0 : BSDF_USE_MATERIAL_IOR;

#if AOV_ID == AOV_ID_DEBUG_OPACITY
#ifdef IS_OPAQUE
    rayPayload.radiance = f16vec3(1.0, 0.0, 0.0); // Distinct from viridis heatmap
    rayPayload.bitfield = uint16_t(0xFFFFu);
#else
    // Payload fields have been set in any-hit shader.
#endif
    return;
#elif AOV_ID == AOV_ID_NORMAL
    rayPayload.radiance = f16vec3((normal + vec3(1.0, 1.0, 1.0)) * 0.5);
    rayPayload.bitfield = uint16_t(0xFFFFu);
    return;
#elif AOV_ID == AOV_ID_DEBUG_TANGENTS
    rayPayload.radiance = f16vec3((shading_state.tangent_u[0] + vec3(1.0, 1.0, 1.0)) * 0.5);
    rayPayload.bitfield = uint16_t(0xFFFFu);
    return;
#elif AOV_ID == AOV_ID_DEBUG_BITANGENTS
    rayPayload.radiance = f16vec3((shading_state.tangent_v[0] + vec3(1.0, 1.0, 1.0)) * 0.5);
    rayPayload.bitfield = uint16_t(0xFFFFu);
    return;
#elif AOV_ID == AOV_ID_DEBUG_BARYCENTRICS
    rayPayload.radiance = f16vec3(1.0 - hit_bc.x - hit_bc.y, hit_bc.x, hit_bc.y);
    rayPayload.bitfield = uint16_t(0xFFFFu);
    return;
#elif AOV_ID == AOV_ID_DEBUG_TEXCOORDS
    rayPayload.radiance = f16vec3(shading_state.text_coords[0]);
    rayPayload.bitfield = uint16_t(0xFFFFu);
    return;
#endif

    /* 3. Apply volume attenuation */
    if (inside && !thin_walled)
    {
        vec3 abs_coeff = mdl_absorption_coefficient(shading_state);
        throughput.x *= float16_t(abs_coeff.x > 0.0 ? exp(-abs_coeff.x * hit_t) : 1.0);
        throughput.y *= float16_t(abs_coeff.y > 0.0 ? exp(-abs_coeff.y * hit_t) : 1.0);
        throughput.z *= float16_t(abs_coeff.z > 0.0 ? exp(-abs_coeff.z * hit_t) : 1.0);
    }

    /* 4. Add Emission */
    {
        Edf_evaluate_data edf_evaluate_data;
        edf_evaluate_data.k1 = -gl_WorldRayDirectionEXT;

#ifdef THIN_WALLED
        if (is_backface)
        {
            mdl_backface_edf_emission_init(shading_state);
            mdl_backface_edf_emission_evaluate(edf_evaluate_data, shading_state);
        }
        else
#endif
        {
            mdl_edf_emission_init(shading_state);
            mdl_edf_emission_evaluate(edf_evaluate_data, shading_state);
        }

        if (edf_evaluate_data.pdf > 0.0)
        {
            vec3 emission_intensity;

#ifdef THIN_WALLED
            if (is_backface)
            {
                emission_intensity = mdl_backface_edf_emission_intensity(shading_state);
            }
            else
#endif
            {
                emission_intensity = mdl_edf_emission_intensity(shading_state);
            }

            radiance += throughput * edf_evaluate_data.edf * emission_intensity;
        }
    }

    // reassign normal, see declaration of variable.
    shading_state.normal = normal;

    /* TODO 5. NEE light sampling */

    /* 6. Russian Roulette */
#ifdef RAND_4D
    float k1 = rng4d_next(rayPayload.rng_state).x;
#else
    float k1 = rng_next(rayPayload.rng_state);
#endif

    uint maxBounces = PC.maxBouncesAndRrBounceOffset >> 16;
    uint rrBounceOffset = PC.maxBouncesAndRrBounceOffset & 0xFFFFu;

    bool terminateRay = false;
    if (bounce == maxBounces - 1)
    {
        terminateRay = true;
    }
    else if (bounce > rrBounceOffset)
    {
        terminateRay = russian_roulette(k1, throughput);
    }

    /* 7. BSDF (importance) sampling. */
    if (!terminateRay)
    {
        Bsdf_sample_data bsdf_sample_data;
        bsdf_sample_data.ior1 = vec3(ior1);
        bsdf_sample_data.ior2 = vec3(ior2);
        bsdf_sample_data.k1 = -gl_WorldRayDirectionEXT;
#ifdef RAND_4D
        bsdf_sample_data.xi = rng4d_next(rayPayload.rng_state);
#else
        bsdf_sample_data.xi[0] = rng_next(rayPayload.rng_state);
        bsdf_sample_data.xi[1] = rng_next(rayPayload.rng_state);
        bsdf_sample_data.xi[2] = rng_next(rayPayload.rng_state);
        bsdf_sample_data.xi[3] = rng_next(rayPayload.rng_state);
#endif

#ifdef THIN_WALLED
        if (is_backface)
        {
            mdl_backface_bsdf_scattering_init(shading_state);
            mdl_backface_bsdf_scattering_sample(bsdf_sample_data, shading_state);
        }
        else
#endif
        {
            mdl_bsdf_scattering_init(shading_state);
            mdl_bsdf_scattering_sample(bsdf_sample_data, shading_state);
        }

        terminateRay = (bsdf_sample_data.event_type == BSDF_EVENT_ABSORB);
        throughput *= bsdf_sample_data.bsdf_over_pdf;

        bool exit_at_back = (bsdf_sample_data.event_type & BSDF_EVENT_TRANSMISSION) != 0;

        if (exit_at_back && !thin_walled)
        {
            inside = !inside;
        }

        rayPayload.ray_dir = bsdf_sample_data.k2;
        rayPayload.ray_origin = offset_ray_origin(shading_state.position, shading_state.geom_normal * (exit_at_back ? -1.0 : 1.0));
    }

    uint bitfield = (uint(inside) << 15) | (bounce + 1);
    rayPayload.bitfield = uint16_t(terminateRay ? 0xFFFFu : bitfield);
    rayPayload.throughput = f16vec3(throughput);
    rayPayload.radiance = f16vec3(radiance);
}
